import "pkg:/source/roku_modules/maestro/view/NodeClass.brs"
import "pkg:/source/roku_modules/maestro/core/Result.brs"
import "pkg:/source/roku_modules/maestro/core/MIOC.brs"
import "pkg:/components/roku_modules/maestro/view/mixin/FocusMixin.brs"

@node("MainScene", "Scene")
class MainScene extends mv.NodeClass
    public navController as mc.types.node
    private launchSequence as mc.types.node

    function new()
        super("MainScene")
    end function

    sub begin()
        ? "begin"
        m.startLaunchSequence()
    end sub

    ' Required to initialize maestro framework
    sub startLaunchSequence()
        m.launchSequence = m.createTask("mc_LaunchSequence", invalid, m.onLaunchSequenceResult)
    end sub

    sub onLaunchSequenceResult(result as mc.Result)
        if (result.isOk)
            ? "launch sequence complete"
            m.navController = m.createSGNode("mv_navController", m.top, "navController")
            m.setInstance(IocInstance.NavController, m.navController)
            mv.setFocus(m.top)
            m.showInitialScreen()
        else
            ? "launch sequence failed"
        end if
    end sub

    sub showInitialScreen()
        initialScreen = m.createSGNode("HomeScreen", invalid, "homeScreen", {
            text: "Initial"
        })
        m.navController@.push(initialScreen)
        initialScreen.setFocus(true)
    end sub
end class
